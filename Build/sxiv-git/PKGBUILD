_pkgname=sxiv
pkgname="${_pkgname}-git"
pkgver=v24.33.ge648c11
pkgrel=1
pkgdesc='Simple X Image Viewer'
arch=('i686' 'x86_64')
url='https://github.com/muennich/sxiv'
license=('GPL2')
depends=('imlib2' 'libexif' 'libxft' 'hicolor-icon-theme')
makedepends=('git')

_patches=('myconf.diff'
          'thumb-fixed-border.diff'
)

source=('git+https://github.com/muennich/sxiv.git'
        ${_patches[@]})

sha1sums=('SKIP'
          'SKIP'
          'SKIP'
)

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${_pkgname}"
	git describe --tags | sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	for patch in ${_patches[@]}; do
		patch < "${srcdir}/$(basename $patch)"
	done
}

build() {
	cd "${_pkgname}"
	make PREFIX='/usr'
}

package() {
	cd "${_pkgname}"
	make         PREFIX='/usr' DESTDIR="$pkgdir" install
	make -C icon PREFIX='/usr' DESTDIR="$pkgdir" install
	install -Dm644 LICENSE      "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README.md    "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm644 sxiv.desktop "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
}

