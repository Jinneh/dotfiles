# vim:ft=zsh

# pass multiple arguments as last single argument to cmd.
# This allows using global aliases on the last argument
#
# cp -v something "${(%):-"%D{%Y%m%d}"}-something.ext"
#
# alias -g TODAY='${(%):-"%D{%Y%m%d}"}'
#    -> 1 -s '-' cp -v something -- TODAY something.ext
# or -> 1 -s '-' cp -v something -- TODAY something .ext
#

local -A opts
opts[-s]=' '
zparseopts -D -K -A opts s: h

integer dd="${@[(I)--]}"

if [[ -v opts[-h] ]] || (( ! dd )); then
	cat <<- EOU
		Usage: ${0} [-h] [-s separator] cmd [cmd_arg]... -- arg...
		  -h               Print this help
		  -s separator     Join arguments with {separator} instead of space

		  Arguments beginning with a dot are appended without the {separator}.
	EOU
	return 0
fi

local -a cmd=( ${@[1, dd - 1]} )

shift $dd
local arg

while (( ARGC > 0 )); do
	if [[ $1 =~ '^\.' || -z "$arg" ]]; then
		arg="${arg}${1}"
	else
		arg="${arg}${opts[-s]}${1}"
	fi
	shift
done

${cmd} "${arg}"


